plugins {
	id 'fabric-loom' version '1.0-SNAPSHOT' apply false
	id 'com.replaymod.preprocess' version 'e1050f4d72'
}

preprocess {
	def mc116 = createNode('1.16.5', 1_16_05, 'yarn')
	def mc117 = createNode('1.17.1', 1_17_01, 'yarn')
	def mc118 = createNode('1.18.2', 1_18_02, 'yarn')
	def mc1192 = createNode('1.19.2', 1_19_02, 'yarn')
	def mc1193 = createNode('1.19.3', 1_19_03, 'yarn')
	def mc1194 = createNode('1.19.4', 1_19_04, 'yarn')

	// mapping difference map
	// base 1194 -> 1193 -> 1192 -> 118 -> 117 -> 116
	// if no mapping difference, just use null instead of file
	mc1194.link(mc1193, file('versions/mapping-1.19.4-1.19.3.txt'))
	mc1193.link(mc1192, file('versions/mapping-1.19.3-1.19.2.txt'))
	mc1192.link(mc118, file('versions/mapping-1.19.2-1.18.1.txt'))
	mc118.link(mc117, file('versions/mapping-1.18.2-1.17.1.txt'))
	mc117.link(mc116, file('versions/mapping-1.17.1-1.16.5.txt'))
}

tasks.register('buildAndGather') {
	subprojects {
		dependsOn project.name + ':build'
	}
	doFirst {
		println 'Building and gathering all subprojects'
		copy {
			subprojects {
				def libDir = project.projectDir.toPath().resolve('build/libs')
				from(libDir) {
					include '*.jar'
					exclude '*-sources.jar', '*-dev.jar'
				}
				into 'build/libs/'
				duplicatesStrategy = DuplicatesStrategy.INCLUDE
			}
		}
	}
}